using utils {forRange}
using string;

//
// Bottles is a lambda function with two parameters:
// - count of type int,
// - n of type string.
// It returns string.
//
bottles = `count `n {
    count == 0
        ? "{n}o more bottles"
        : "{count} bottle{count != 1 ? "s"}"   // This half-condition adds 's' to 'bottle' if count!=1
};

forRange(-99, 1) `count
    sys_log("{}\
        { bottles(-count, "N") } of beer on the wall, { bottles(-count, "n") } of beer.
        { count == 0
            ? "Go to the store and buy some more"
            : "Take one down and pass it around"
        }, { bottles((-count-1+100) % 100, "n") } of beer on the wall.
    ");
// The loop body above has a multiline string with nested interpolations